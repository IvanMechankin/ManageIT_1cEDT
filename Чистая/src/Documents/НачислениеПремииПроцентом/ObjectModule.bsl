
Процедура ОбработкаПроведения(Отказ, Режим)

	
	
	// регистр Зарплата
	Движения.Зарплата.Очистить();
	Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
		Движение = Движения.Зарплата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом;
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = КонецМесяца(Дата);
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.Сотрудник = ТекСтрокаСотрудники.Сотрудник;
		Движение.Показатель = ПроцентПремии;
	КонецЦикла;
	
	Движения.Зарплата.Записать();
	
	РасчётПремииПроцентом();
	
	
	
	

КонецПроцедуры 




Процедура РасчётПремииПроцентом()
	
	Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ЗарплатаБазаЗарплата.НомерСтроки КАК НомерСтроки,
			|	ЗарплатаБазаЗарплата.СуммаБаза КАК СуммаБаза,
			|	ЗарплатаБазаЗарплата.Показатель КАК Показатель
			|ИЗ
			|	РегистрРасчета.Зарплата.БазаЗарплата(
			|			&Измерения,
			|			&Измерения,
			|			,
			|			Регистратор = &Регистратор
			|				И ВидРасчета = &ВидРасчета) КАК ЗарплатаБазаЗарплата");
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом);
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	База = Запрос.Выполнить().Выбрать();
	
	Пока База.Следующий() Цикл
		
		Движение = Движения.Зарплата[База.НомерСтроки - 1]; 
		Результат = База.СуммаБаза * База.Показатель / 100; 
		Движение.Сумма = Результат;
		
		
		
	КонецЦикла;
	
	Движения.Зарплата.Записать(, Истина);
	
			
	
КонецПроцедуры
